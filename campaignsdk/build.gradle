apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
group = 'com.loyagram.android'
archivesBaseName = 'campaign-sdk'
version = '0.0.7'


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def mavenUserName = properties.getProperty('mavenRepoUserName')
def mavenPassword = properties.getProperty('mavenRepoPassword')
def mavenWriteUrl = properties.getProperty('myMavenRepoWriteUrl')

publishing {
    repositories {
        maven {
            url mavenWriteUrl
            credentials {
                username mavenUserName
                password mavenPassword
            }
        }
    }

    publications {
        aar(MavenPublication) {
            groupId group
            artifactId archivesBaseName
            version project.version
            artifact("$buildDir/outputs/aar/${archivesBaseName}-release.aar")

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.getByName("_releaseCompile").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', it.moduleVersion)
                }
            }
        }
    }
}
ext {
    bintrayRepo = 'maven'
    bintrayName = 'campaign-sdk'
    bintrayOrg = 'loyagram'
    publishedGroupId = 'com.loyagram.android'
    libraryName = 'Campaign Sdk'
    artifact = 'campaign-sdk'

    libraryDescription = 'Loyagram android sdk'

    siteUrl = 'https://github.com/Loyagram/campaign-sdk'
    gitUrl = 'https://github.com/Loyagram/campaign-sdk.git'
    githubRepository = 'Loyagram/campaign-sdk'

    libraryVersion = '0.0.7'

    developerId = 'sandhil'
    developerName = 'sandhil eldhose'
    developerEmail = 'sandhil@loyagram.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}
android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 7
        versionName "0.0.7"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'RestrictedApi'
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.google.code.gson:gson:2.8.0'
}

apply from: 'bintray.gradle'
apply from: 'install.gradle'